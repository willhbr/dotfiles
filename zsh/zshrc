#!/bin/zsh

source ~/.shell/autoload.sh

# Prompt
autoload -U colors
colors
setopt prompt_subst
PROMPT='%(?.%F{$HOST_COLOR}.%F{red})>%f '

# History
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_DUPS
setopt HISTIGNORESPACE
HISTSIZE=1000000
SAVEHIST=1000000
HISTFILE=~/._zsh_history
setopt inc_append_history share_history

REPORTTIME=10

# Vim key bindings

bindkey -v
export KEYTIMEOUT=50

bindkey -M viins 'jk' vi-cmd-mode
bindkey -M viins 'Jk' vi-cmd-mode
bindkey -M viins 'JK' vi-cmd-mode
bindkey -M viins 'jK' vi-cmd-mode
# Allow deleting into non-inserted text
bindkey "^?" backward-delete-char
# I do "jkk" to run the prev command, and if I miss the j I want it to still work
alias kk=r

precmd() {
  if [ -z "$TMUX" ]; then return; fi
  local session="$(tmux display -p '#S')"
  tmux set-environment -h -t "$session" "START_TIME_$TMUX_PANE" "$(date +%s)"
}

cmd-times() {
  local now="$(date +%s)"
  tmux list-panes -F '#{pane_id}' | while read -r pane_id; do
    local cmd="$(tmux display -t "$pane_id" -p '#{pane_current_command}')"
    local start="$(tmux show-environment "START_TIME_$pane_id")"
    local t="${start#"START_TIME_$pane_id="}"
    local diff=$(( now - t ))
    echo "$cmd: $(date -u -d @"$diff" +'%-Mm %-Ss')"
  done
}

function zle-line-init zle-keymap-select {
  VIM_PROMPT="%F{252}î‚²%F{0}%K{252} NORMAL %f%k"
  RPS1="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/}"
  zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

# This autoload is required on macOS for completion (For some reason)
autoload -U compinit && compinit
source ~/.zsh/tmux_complete.zsh


export HIGHLIGHTING_ENABLED=true
# Load config specific to this device
__local_file="$HOME/.zshrc-local" 
if [ -e $__local_file ]; then
  source $__local_file
fi

source ~/.shell/welcome.sh

# Pug installed
source "$HOME/.pug/source/zsh/pug"

if [ -z "$TMUX" ]; then
  case $- in
    *i*) tmux attach ;;
  esac
fi

export FZF_TMUX_OPTS="-p"
export FZF_CTRL_R_OPTS="--reverse --preview 'echo {}' --preview-window down:3:hidden:wrap --bind '?:toggle-preview'"
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
